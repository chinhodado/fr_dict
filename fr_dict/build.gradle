apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "chin.com.frdict"
        minSdkVersion 19
        // If targetSdkVersion >= 29, the path returned by Environment.getExternalStorageDirectory()
        // won't work
        targetSdkVersion 28
        versionCode 3
        versionName "2.1"
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            versionNameSuffix " build " + getDate()
        }
        debug {
            versionNameSuffix " build " + getDate()
        }
    }
    namespace 'chin.com.frdict'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation project(':common')
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}
